package mop;

import java.io.*;

/**
 * Warns if length() is invoked on a File object representing a directory.
 *
 * The return value of length() is unspecified if the File instance denotes a
 * directory.
 * http://download.oracle.com/javase/6/docs/api/java/io/File.html#length%28%29
 *
 * This propery warns if length() is called when a File object represents a
 * directory.
 */

File_LengthOnDirectory(File f) {
	event isdirectory_true after(File f) returning(boolean r) :
		call(* File+.isDirectory(..)) && target(f) && condition(r) {}
	event isfile_false after(File f) returning(boolean r) :
		call(* File+.isFile(..)) && target(f) && condition(!r) {}
	event length before(File f) : call(* File+.length(..)) && target(f) {}

	fsm :
		initial [
			isdirectory_true -> directory
			isfile_false -> directory
		]
		directory [
			length -> error
		]
		error [
		]

	@error {
		System.err.println("length() was invoked on a File instance that may represent a directory.");
	}
}
